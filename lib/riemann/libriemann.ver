RIEMANN_C_LOCAL {
local:
        *;
};

RIEMANN_C_1.0 {
        riemann_client_version;
        riemann_client_version_string;

        riemann_attribute_new;
        riemann_attribute_create;
        riemann_attribute_free;
        riemann_attribute_set_key;
        riemann_attribute_set_value;
        riemann_attribute_set;

        riemann_query_new;
        riemann_query_free;
        riemann_query_set_string;

        riemann_event_init;
        riemann_event_new;
        riemann_event_create;
        riemann_event_free;
        riemann_event_set;
        riemann_event_tag_add;
        riemann_event_attribute_add;

        riemann_message_new;
        riemann_message_create_with_events;
        riemann_message_create_with_query;
        riemann_message_free;
        riemann_message_set_events_n;
        riemann_message_set_events;
        riemann_message_set_query;
        riemann_message_to_buffer;
        riemann_message_from_buffer;

        riemann_client_new;
        riemann_client_create;
        riemann_client_free;
        riemann_client_connect;
        riemann_client_disconnect;
        riemann_client_send_message;
        riemann_client_send_message_oneshot;
        riemann_client_recv_message;
} RIEMANN_C_LOCAL;

RIEMANN_C_1.1 {
        riemann_send;
        riemann_query;
} RIEMANN_C_1.0;

RIEMANN_C_1.2 {
        riemann_client_get_fd;
} RIEMANN_C_1.1;

RIEMANN_C_1.3 {
        riemann_event_set_va;
        riemann_event_create_va;

        riemann_message_set_events_va;
        riemann_message_create_with_events_va;

        riemann_send_va;
} RIEMANN_C_1.2;

RIEMANN_C_1.4 {
        riemann_message_append_events_n;
        riemann_message_append_events;
        riemann_message_append_events_va;

        riemann_attribute_clone;
        riemann_event_clone;
        riemann_query_clone;
        riemann_message_clone;
} RIEMANN_C_1.3;

RIEMANN_C_1.5 {
        riemann_client_create;
        riemann_client_connect;
} RIEMANN_C_1.4;

RIEMANN_C_1.6 {
        riemann_message_get_packed_size;

        riemann_event_string_attribute_add;

        riemann_communicate;
} RIEMANN_C_1.5;

RIEMANN_C_1.7 {
        riemann_communicate_query;
        riemann_communicate_event;
} RIEMANN_C_1.6;

RIEMANN_C_1.8 {
        riemann_client_set_timeout;
} RIEMANN_C_1.7;

RIEMANN_C_1.10 {
        riemann_client_create;
        riemann_client_new;
} RIEMANN_C_1.8;
